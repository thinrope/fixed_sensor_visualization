#!/usr/bin/gnuplot
reset;
set encoding utf8;

LABEL=system("cat cache/nGeigie_map.csv|cut -d, -f1-10|grep -P '^" . ID . ",' |perl -MText::ParseWords -CS -ne '@R=&quotewords(\",\", 0, $_); print \"$R[5], $R[6]\n\"'");
CPM2DRE=system("cat cache/nGeigie_map.csv|cut -d, -f1,2|grep -P '^" . ID . ",' |cut -d, -f2");
CPM2DRE=CPM2DRE + 0.0;
DATAFILE=sprintf("cache/%d.csv", ID);
OUTFILE=sprintf("out/%d.png", ID);
OUTFILE_window=sprintf("out/%d_window.png", ID);
OUTFILE_small=sprintf("out/%d_small.png", ID);
TABLE=sprintf("tmp/%d.data", ID);

# This converts the time string in column COL to a number of seconds
iso2s(COL) = strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, strcol(COL));
s2iso(seconds) = strftime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, seconds);

set datafile separator ",";

# set palette
set cbtics (0.03, 0.14, 0.43, 1.65, 65.54);
set palette model RGB;
set palette file "cyanhalo_lut_4xlog10.csv" u 1:2:3;
set cbrange [0.03: 65.54];
unset colorbox;


set key autotitle columnheader top right;
set grid xtics front;
S_START=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, PERIOD_START);
S_END=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, PERIOD_END);
END_YEAR=system("TZ=" . CONFIG_TIMEZONE . " date +%Y --date='" . PERIOD_END . "'");

set xrange [ S_START : S_END ]; # set xlabel "timestamp";
stats DATAFILE u (iso2s(1)):2 nooutput;
three_sigma_percent=sprintf("±%0.1f%%",	100.0 * 3*STATS_stddev_y / STATS_mean_y);

min_y = (1.2*STATS_min_y < STATS_mean_y - 4.1*STATS_stddev_y) ? 1.2*STATS_min_y : STATS_mean_y - 4.1*STATS_stddev_y; min_y = (min_y > 0) ? min_y : 0;
max_y = (1.2*STATS_max_y > STATS_mean_y + 4.1*STATS_stddev_y) ? 1.2*STATS_max_y : STATS_mean_y + 4.1*STATS_stddev_y;
#min_y = 0; max_y = 350;
set yrange [min_y: max_y]; set ytics nomirror textcolor rgb "dark-green"; set label 31 "{/*1.1 CPM}" at graph 0.0, 1.04 right textcolor rgb "dark-green";
set arrow 1 nohead front from graph 0, graph 0 to graph 0, graph 1 lc rgb "dark-green";
set y2range [min_y * CPM2DRE: max_y * CPM2DRE]; set y2tics nomirror textcolor rgb "blue"; set label 32 "{/*1.1 μSv/h}" at graph 1.0, graph 1.04 left textcolor rgb "blue";
set arrow 2 nohead front from graph 1, graph 0 to graph 1, graph 1 lc rgb "blue";

set xdata time; set timefmt "%Y-%m-%dT%H:%M:%S" . CONFIG_TZ; set format x "%Y-%m-%d\n%H:%M " . CONFIG_TZ;
set format y "% 0.0f"; set format y2 "%0.2f";

# Large PNGs for gamma.tar.bz
FONT="Arial Unicode MS";
set term png enhanced notransparent nointerlace truecolor butt font FONT . ",8" size CONFIG_WIDTH_BIG, CONFIG_HEIGHT_BIG background "#ffffef";
set output OUTFILE;

set object 1 rectangle from "2013-01-01", STATS_mean_y - 3*STATS_stddev_y to "2033-01-01", STATS_mean_y + 3*STATS_stddev_y back fc rgb "cyan" fillstyle solid 0.2 border lc rgb "cyan";
set title sprintf("&{%0.2f±%0.2f μSv/h (%0.1f±%0.1f CPM), total %d measurements}",	STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE, STATS_mean_y, 3*STATS_stddev_y, STATS_records) font FONT . ",9";
set label 1 sprintf("%0.2f±%0.2f μSv/h &{(%0.1f±%0.1f CPM), total %d measurements}",	STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE, STATS_mean_y, 3*STATS_stddev_y, STATS_records) at screen 0.5, screen 0.93 center font FONT . ",9" tc rgb "blue";
set label 2 sprintf("&{%0.2f±%0.2f μSv/h (}%0.1f±%0.1f CPM&{), total %d measurements}",	STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE, STATS_mean_y, 3*STATS_stddev_y, STATS_records) at screen 0.5, screen 0.93 center font FONT . ",9" tc rgb "dark-green";
set label 3 sprintf("&{%0.2f±%0.2f μSv/h }(&{%0.1f±%0.1f CPM}), total %d measurements",	STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE, STATS_mean_y, 3*STATS_stddev_y, STATS_records) at screen 0.5, screen 0.93 center font FONT . ",9" tc rgb "black";

#set label 13 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0215, graph 0.940 font FONT . ",15" textcolor rgb "black";
set label 14 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0235, graph 0.930 font FONT . ",15" textcolor rgb "dark-gray";
set label 15 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0240, graph 0.925 font FONT . ",15" textcolor rgb "#FF0066";
set label 16 LABEL at graph 0.0240, graph 0.84 font FONT . ",9" textcolor rgb "#FF0066";
set style line 1 lc rgb "orange";
# FIXME: the "24-bin SMA [= 2 h ]" label below is hardcoded
plot \
	DATAFILE u 1:2 w p ls 1 notitle, \
	DATAFILE u 1:($4*CPM2DRE):($4*CPM2DRE) axes x1y2 w l lw 2 palette title "24-bin SMA [= 2 h ]", \
	DATAFILE u 1:($3*CPM2DRE):($3*CPM2DRE) axes x1y2 w l lw 1 lc rgbcolor "#000000" notitle, \
	-100 w filledcurves fc rgb "cyan" fs transparent solid 0.2 title "±3{/Arial-Unicode-MS σ} area [= " . three_sigma_percent . " ]";
unset for [i in "1"] object i; unset for [i in "1 2"] arrow i; unset for [i in "1 2 3 13 14 15 16 31 32"] label i; unset title;

# small PNGs for Fusion tables
FONT="Futura Std Medium";
set term png enhanced notransparent nointerlace truecolor butt font FONT . ",7" size CONFIG_WIDTH_SMALL, CONFIG_HEIGHT_SMALL background "#ffffff";
set output OUTFILE_small;

uSv_h = sprintf("%0.2f ± %0.2f", STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE);
set title sprintf("%s μSv/h (%0.1f ± %0.1f CPM)", uSv_h, STATS_mean_y, 3*STATS_stddev_y) font FONT . ",9";
unset ytics; unset y2tics;
set yrange [0: 1.1]; set ytics mirror; set label 31 "μSv/h" at graph 0.0, graph 1.0 right offset -0.5, 0.5; set format y "%0.1f";
set grid x y;
set format x "%m/%d"; set xtics offset 0, graph 0.03; set label 32 END_YEAR at graph 0.5, graph -0.17 center;

plot \
	DATAFILE u 1:($4*CPM2DRE):($4*CPM2DRE) w l lw 1 palette notitle;
unset title; unset for [i in "31 32"] label i;


# small PNGs for Fusion tables @2x res
FONT="Futura ND";
WIDTH=CONFIG_WIDTH_SMALL * 2; HEIGHT = CONFIG_HEIGHT_SMALL * 2;
set term pngcairo enhanced color solid notransparent background "#ffffff" font FONT . ",14" linewidth 2 butt size WIDTH, HEIGHT;
set output sprintf("out/%d_%dx%d.png", ID, WIDTH, HEIGHT);

uSv_h = sprintf("%0.2f ± %0.2f", STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE);
set title sprintf("%s μSv/h (%0.1f ± %0.1f CPM)", uSv_h, STATS_mean_y, 3*STATS_stddev_y) font FONT . ",18";
set yrange [0: 1.1]; set ytics mirror; set label 31 "μSv/h" at graph 0.0, graph 1.0 right offset -0.5, 0.5; set format y "%0.1f";
set grid x y;
set format x "%m/%d"; set xtics offset 0, graph 0.03; set xlabel END_YEAR offset 0,1;
# add sensor ID within the graph
set label 40 sprintf("%d", ID) at graph 0.5, graph 0.75 center font FONT . ",100" back textcolor rgb "#dddddd";
plot \
	DATAFILE u 1:($4*CPM2DRE):($4*CPM2DRE) w l lw 1 palette notitle;

unset title; unset for [i in "31 32 40"] label i; unset xlabel;


# save uSv/h data for ALL.png
set format x "%Y-%m-%dT%H:%M:%S" . CONFIG_TZ; set format y "%0.3f";
set table TABLE;
plot DATAFILE u 1:($4*CPM2DRE) title sprintf("%d: %s [%.3E]", ID, uSv_h, CPM2DRE);
unset table;

# update window graphs
FONT="Arial Unicode MS";
set term png enhanced notransparent nointerlace truecolor butt font FONT . ",8" size CONFIG_WIDTH_BIG, CONFIG_HEIGHT_BIG background "#ffffef";
set output OUTFILE_window;
win_default=5;
set title "Update window for sensor " . ID .", in number of intervals (default τ= ". win_default . " min)";
#set label 13 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0215, graph 0.940 font FONT . ",15" textcolor rgb "black";
set label 14 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0235, graph 0.930 font FONT . ",15" textcolor rgb "dark-gray";
set label 15 sprintf("%d:  %0.2f μSv/h", ID, STATS_mean_y * CPM2DRE) at graph 0.0240, graph 0.925 font FONT . ",15" textcolor rgb "#FF0066";
set label 16 LABEL at graph 0.0240, graph 0.84 font FONT . ",9" textcolor rgb "#FF0066";
set format x "%m/%d"; set xtics offset 0, graph 0.03; set xlabel END_YEAR offset 0,1;
# default update in minutes
set yrange[0:10]; set ytics 0, 1; set format y "%.0fτ";
plot \
	DATAFILE u 1:($5/60/win_default) w l lw 1 lc rgb "blue" notitle;
unset ytics; unset ylabel; unset title; unset for [i in "13 14 15 16"] label i
